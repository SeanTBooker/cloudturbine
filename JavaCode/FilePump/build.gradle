
// JAR settings
jar {
    // Include NOTICE.txt in the JAR's META-INF directory
    into("META-INF") {
        from "$projectDir/NOTICE.txt"
    }
}
// Specify ".doFirst" to force this to run in the Execution phase.  Without
// this specification, this would be run during the Configuration phase, which
// would fail if CTlib.jar didn't yet exist; at Execution phase, CTlib.jar
// should have been created already.
jar.doFirst {
    // Create a "fat jar" file which includes all the dependencies; that way,
    // the jar can be run stand-alone with none of the other dependency jars
    // around.  The following was taken from a nice simple example found at:
    // https://newfivefour.com/gradle-hello-world-java-fat-jar.html
    // The "it" variable (kind of like "this" in Java) is a Groovy-ism which
    // represents the implicit variable given to this closure, which I think is
    // the project object.
    from {
        (configurations.runtime).collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
    
    // Build up a string containing all the runtime dependencies; this will be
    // used as the 'Class-Path' string in the manifest instead of hard-wiring
    // library names
    StringBuilder sb = new StringBuilder();
    project.configurations.runtime.each {
        sb.append(it.name + ' ')
    }
    String runtimeDependenciesStr = sb.toString().trim()
    
    manifest {
        attributes 'Main-Class': 'erigo.filepump.FilePump',
                   'Class-Path': runtimeDependenciesStr
    }
}

repositories {
    flatDir {
    	// Location of CTlib.jar
    	dirs "$rootProject.projectDir/CTlib/build/libs"
    }
}

dependencies {
	// FilePump doesn't currently depend on CTlib
	// Build CTlib project first, because we use CTlib.jar
	// compile project(':CTlib')
	// Specify that we use CTlib.jar
    // compile name: 'CTlib'
    compile group: 'commons-cli', name: 'commons-cli', version: '1.3.1'
    compile group: 'commons-logging', name: 'commons-logging', version: '1.2'
    compile group: 'commons-net', name: 'commons-net', version: '3.5'
    compile group: 'org.apache.commons', name: 'commons-vfs2', version: '2.0'
    compile group: 'com.jcraft', name: 'jsch', version: '0.1.54'
}

