
// JAR settings
// Specify ".doFirst" to force this to run in the Execution phase.  Without
// this specification, this would be run during the Configuration phase, which
// would fail if CTlib.jar didn't yet exist; at Execution phase, CTlib.jar
// should have been created already.
jar.doFirst {
    // Create a "fat jar" file which includes all the dependencies; that way,
    // the jar can be run stand-alone with none of the other dependency jars
    // around.  The following was taken from a nice simple example found at:
    // https://newfivefour.com/gradle-hello-world-java-fat-jar.html
    // The "it" variable (kind of like "this" in Java) is a Groovy-ism which
    // represents the implicit variable given to this closure, which I think is
    // the project object.
    from {
        (configurations.runtime).collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
    
    // Build up a string containing all the runtime dependencies; this will be
    // used as the 'Class-Path' string in the manifest instead of hard-wiring
    // library names
    StringBuilder sb = new StringBuilder();
    project.configurations.runtime.each {
        sb.append(it.name + ' ')
    }
    String runtimeDependenciesStr = sb.toString().trim()

    // manifest {
    //     attributes 'Main-Class': 'erigo.ctstream.CTstream',
    //                'Class-Path': runtimeDependenciesStr
    // }
    // Only thing we really need on the class-path is CTweb.jar;
    // this is so CTstream can launch the CTweb server.
    // Need to have CTweb.jar in the same folder where CTstream.jar
    // is located.
    manifest {
        attributes 'Main-Class': 'erigo.ctstream.CTstream',
                   'Class-Path': 'CTweb.jar'
    }

}

repositories {
    flatDir {
    	// Location of CTlib.jar
    	dirs "$rootProject.projectDir/CTlib/build/libs"
        // Location of CTweb.jar
        // This line is only needed if we are bundling CTweb.jar into this fat CTstream.jar
        // dirs "$rootProject.projectDir/CTweb/build/libs"
    }
    mavenCentral()
    // The following repository is required to fetch a dependency for 'core-video-capture'
    // (specifically, to fetch the required version of the com.nativelibs4java library "bridj")
    // See http://openimaj.org/openimaj-hardware/core-video-capture/dependencies.html for dependency info for the OpenIMAJ library
	maven {
    	url "http://maven.openimaj.org"
    }
}

dependencies {
	// Build CTlib project first, because we use CTlib.jar
	compile project(':CTlib')
	// Specify that we use CTlib.jar
    compile name: 'CTlib'

    // Build CTweb project first, because we use CTweb.jar
    // This line is only needed if we are bundling CTweb.jar into this fat CTstream.jar
    // compile project(':CTweb')
    // Specify that we use CTweb.jar
    // compile name: 'CTweb'

    compile group: 'commons-cli', name: 'commons-cli', version: '1.3.1'

    // compile group: 'com.github.sarxos', name: 'webcam-capture', version: '0.3.11+'
    // compile group: 'org.slf4j', name: 'slf4j-nop', version: '1.8+'

    // 2019-09-17  JPW  update 'core-video-capture' library version from '1.3.1' to '1.3.8'
    // Here's the original line:
    // compile group: 'org.openimaj', name: 'core-video-capture', version: '1.3.1'
    // Here's the new dependency specification:
    //   NOTE: need the "exclude" line shown below (at least when working under Gradle 4.4);
    //         without this exclude, the build results in an error over a POM file vs. JAR file confusion
    //         See the following URLs for more info:
    //         https://stackoverflow.com/questions/32361545/gradle-trying-to-unzip-a-pom-typed-dependency
    //         https://discuss.gradle.org/t/how-do-i-exclude-specific-transitive-dependencies-of-something-i-depend-on/17991
    //         https://github.com/gradle/gradle/issues/8582
    compile(group: 'org.openimaj', name: 'core-video-capture', version: '1.3.8') {
        exclude group: 'com.github.fommil.netlib', module: 'all'
    }

    // JFreeChart was used for preview windows of data streams (such as audio);
    // problem is, it is covered by LGPL.  No longer include this feature.
    // compile group: 'org.jfree', name: 'jfreechart', version: '1.0.19'
}
